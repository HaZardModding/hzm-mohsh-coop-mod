//---------------------------------------------------------------------------------
//	gags/T1L1_PlaneRide.Scr
//	Jeff Leggett
//	08/01/2002
//
//	Implementation of the opening plane ride sequence in 1-1.
//
//---------------------------------------------------------------------------------

//---------------------------------------------------------------------------------
main:
//	initialization...
//
//	Call this before waittill spawn and after waittill prespawn
//---------------------------------------------------------------------------------

	exec global/fadein.scr
	level.bSkipPlaneRide	= 0

	// make sure only AA fire destroys this truck..
	$truck_1 immune bullet grenade rocket

	// hide the parachute that's on the barn.
	$barn_parachute hide

End

//---------------------------------------------------------------------------------
SkipPlaneRide:
//
//	Skip plane ride.. Make sure player exits at the same place as if we did the plane ride...
//---------------------------------------------------------------------------------
	
	fadein 0.1 0 0 0 1

	$player notsolid

	waitthread SetupPlayer
	waitthread PlayerStandUp
	waitthread PlayerMoveForward
	waitthread PlayerJump

	$player solid

End
//---------------------------------------------------------------------------------
PlayStartSounds:
//---------------------------------------------------------------------------------
//	wait 11.25
	waitframe
	waitframe
	
	$jumpmaster playsound t1_planefront
	$airborne[12] playsound t1_planeback
	
end

//---------------------------------------------------------------------------------
DoPlaneRide:
//
//	Start it up....
//---------------------------------------------------------------------------------
	fadeout .001 0 0 0 1

	$noparachute_guy_start hide
	$noparachute_guy_start notsolid
	$noparachute_guy_dest hide
	$noparachute_guy_dest notsolid

	$the_plane notsolid
	$the_plane surface "all" "+nodraw"
	$jumpmaster attach $the_plane tag_captain 1 ( 0 0 0 )

	$the_plane thread global/LoopAnim.Scr::LoopAnim idle

//	waitthread JumpLightInit

	//$front_plane playsound t1_planefront
	thread PlayStartSounds

	$airborne[1]  waitthread Setup1 11A101_airman01Idle 11A102_airman01StandUp 11A103_airman01StandIdle 11A104_airman01HookUp 11A105_airman01StartCheck seat1
	$airborne[2]  waitthread Setup1 11A201_airman02Idle 11A202_airman02StandUp 11A203_airman02StandIdle 11A204_airman02HookUp 11A205_airman02StartCheck seat2
	$airborne[3]  waitthread Setup1 11A301_airman03Idle 11A302_airman03StandUp 11A303_airman03StandIdle 11A304_airman03HookUp 11A305_airman03StartCheck seat3
	$airborne[4]  waitthread Setup1 11A401_airman04Idle 11A402_airman04StandUp 11A403_airman04StandIdle 11A404_airman04HookUp 11A405_airman04StartCheck seat4
	$airborne[5]  waitthread Setup1 11A501_airman05Idle 11A504_Airman05StandUp 11A505_Airman05StandIdle 11A506_Airman05HookUp 11A508_Airman05StartCheck seat6
	$airborne[6]  waitthread Setup1 11A601_airman06Idle 11A604_Airman06StandUp 11A605_Airman06StandIdle 11A606_Airman06HookUp 11A608_Airman06StartCheck seat7
	
	$airborne[7]  waitthread Setup1 11A701_airman07Idle 11A704_Airman07StandUp 11A705_Airman07StandIdle 11A706_Airman07HookUp 11A708_Airman07StartCheck seat8
	$airborne[8]  waitthread Setup1 11A801_airman08Idle 11A804_Airman08StandUp 11A805_Airman08StandIdle 11A806_Airman08HookUp 11A808_Airman08StartCheck seat9
	$airborne[9]  waitthread Setup1 11A951_airman09Idle 11A954_Airman09StandUp 11A955_Airman09StandIdle 11A956_Airman09HookUp 11A958_Airman09StartCheck seat10
	$airborne[10] waitthread Setup1 11A051_airman10Idle 11A054_Airman10StandUp 11A055_Airman10StandIdle 11A056_Airman10HookUp 11A058_Airman10StartCheck seat11
	$airborne[11] waitthread Setup1 11A151_airman11Idle 11A154_Airman11StandUp 11A155_Airman11StandIdle 11A156_Airman11HookUp 11A158_Airman11StartCheck seat12
	$airborne[12] waitthread Setup1 11A251_airman12Idle 11A254_Airman12StandUp 11A255_Airman12StandIdle 11A256_Airman12HookUp 11A258_Airman12StartCheck seat13
	
	$airborne[1]  waitthread Setup2 11A106_airman01CheckCycle 11A107_airman01SoundOff 11A108_airman01Walk 11A109_airman01Jump 11A110_airman01Balance
	$airborne[2]  waitthread Setup2 11A206_airman02CheckCycle 11A207_airman02SoundOff 11A208_airman02Walk 11A209_airman02Jump 11A210_airman02Balance
	$airborne[3]  waitthread Setup2 11A306_airman03CheckCycle 11A307_airman03SoundOff 11A308_airman03Walk 11A309_airman03Jump 11A310_airman03Balance
	$airborne[4]  waitthread Setup2 11A406_airman04CheckCycle 11A407_airman04SoundOff 11A408_airman04Walk 11A409_airman01KickedOff 11A410_airman04Balance
	$airborne[5]  waitthread Setup2 11A509_Airman05CheckCycle 11A510_Airman05SoundOff 11A514_Airman05Walk NOJUMP 11A511_Airman05Balance
	$airborne[6]  waitthread Setup2 11A609_Airman06CheckCycle 11A610_Airman06SoundOff 11A614_Airman06Walk NOJUMP 11A611_Airman06Balance
	$airborne[7]  waitthread Setup2 11A709_Airman07CheckCycle 11A710_Airman07SoundOff 11A714_Airman07Walk NOJUMP 11A711_Airman07Balance
	$airborne[8]  waitthread Setup2 11A809_Airman08CheckCycle 11A810_Airman08SoundOff 11A814_Airman08Walk NOJUMP 11A811_Airman08Balance
	$airborne[9]  waitthread Setup2 11A959_Airman09CheckCycle 11A960_Airman09SoundOff 11A964_Airman09Walk NOJUMP 11A961_Airman09Balance
	$airborne[10] waitthread Setup2 11A059_Airman10CheckCycle 11A060_Airman10SoundOff 11A064_Airman10Walk NOJUMP 11A061_Airman10Balance
	$airborne[11] waitthread Setup2 11A159_Airman11CheckCycle 11A160_Airman11SoundOff 11A164_Airman11Walk NOJUMP 11A161_Airman11Balance
	$airborne[12] waitthread Setup2 11A259_Airman12CheckCycle 11A260_Airman12SoundOff 11A264_Airman12Walk NOJUMP 11A261_Airman12Balance
	
	$airborne[1]  waitthread Setup3 11A111_airman01ReactExp 11A112_airman01HellYes 11A113_airman01TransLook 11A114_airman01LookIdle 11A115_airman01StandHookIdle
	$airborne[2]  waitthread Setup3 11A211_airman02ReactExp 11A212_airman02HellYes 11A213_airman02TransLook 11A214_airman02LookIdle 11A215_airman02StandHookIdle
	$airborne[3]  waitthread Setup3 11A311_airman03ReactExp 11A312_airman03HellYes 11A313_airman03TransLook 11A314_airman03LookIdle 11A315_airman03StandHookIdle
	$airborne[4]  waitthread Setup3 11A411_airman04ReactExp 11A412_airman04HellYes 11A413_airman04TransLook 11A414_airman04LookIdle 11A415_airman04StandHookIdle
	$airborne[5]  waitthread Setup3 11A512_Airman05ReactExp 11A513_Airman05HellYes 11A502_Airman05TransLook 11A503_Airman05LookIdle 11A507_Airman05StandHookIdle
	$airborne[6]  waitthread Setup3 11A612_Airman06ReactExp 11A613_Airman06HellYes 11A602_Airman06TransLook 11A603_Airman06LookIdle 11A607_Airman06StandHookIdle
	$airborne[7]  waitthread Setup3 11A712_Airman07ReactExp 11A713_Airman07HellYes 11A702_Airman07TransLook 11A703_Airman07LookIdle 11A707_Airman07StandHookIdle
	$airborne[8]  waitthread Setup3 11A812_Airman08ReactExp 11A813_Airman08HellYes 11A802_Airman08TransLook 11A803_Airman08LookIdle 11A807_Airman08StandHookIdle
	$airborne[9]  waitthread Setup3 11A962_Airman09ReactExp 11A963_Airman09HellYes 11A952_Airman09TransLook 11A953_Airman09LookIdle 11A957_Airman09StandHookIdle
	$airborne[10] waitthread Setup3 11A062_Airman10ReactExp 11A063_Airman10HellYes 11A052_Airman10TransLook 11A053_Airman10LookIdle	11A057_Airman10StandHookIdle
	$airborne[11] waitthread Setup3 11A162_Airman11ReactExp 11A163_Airman11HellYes 11A152_Airman11TransLook 11A153_Airman11LookIdle	11A157_Airman11StandHookIdle
	$airborne[12] waitthread Setup3 11A262_Airman12ReactExp 11A263_Airman12HellYes 11A252_Airman12TransLook 11A253_Airman12LookIdle 11A257_Airman12StandHookIdle

	// set these 3 guys up to do anim_attach for the check gear animations... (and soundoff anim...)
	$airborne[2].check_anim_attach = 1
	$airborne[3].check_anim_attach = 1
	$airborne[4].check_anim_attach = 1

	// setup some guys to look at france...
	$airborne[3].look_at_france = 11A316_Airman03ReactAirman02
	$airborne[4].look_at_france = 11A416_Airman04ReactAirman02
	$airborne[5].look_at_france = 11A515_Airman05ReactAirman02
	$airborne[6].look_at_france = 11A615_Airman06ReactAirman02
	$airborne[7].look_at_france = 11A715_Airman07ReactAirman02
	$airborne[8].look_at_france = 11A815_Airman08ReactAirman02

	if ( level.bSkipPlaneRide==1 )
	{
		thread SkipPlaneRide
			End
	}

	thread global/fadein.scr::DoFadeIn t1l1_1 t1l1_2 t1l1_3 t1l1_4 0

	thread SetupPlayer
	$jumpmaster thread DoJumpMaster

	freezeplayer
	$player.viewangles = "0 0 0"
	wait 13
	releaseplayer
	$player.viewangles = "0 0 0"

End

//---------------------------------------------------------------------------------
Cleanup:
//
//	Delete all the guys and the plane hull....
//---------------------------------------------------------------------------------

	$the_plane delete

	$jumpmaster delete

//	$jumplight delete
	
	for (local.i=$airborne.size;local.i>0;local.i--)
	{
		$airborne[local.i] delete
	}

	$player physics_on

end

//---------------------------------------------------------------------------------
DoJumpMaster:
//---------------------------------------------------------------------------------

	self exec global/disable_ai.scr
	self physics_off
	self thread global/LoopAnim.Scr::LoopAnim 11J803_JumpmasterStandIdle
	wait 15
	thread DoWelcomeToFrance
	wait 14
	thread JumpLightRed
	wait 1

	self waitthread global/LoopAnim.Scr::LoopAnimStop
///*
	thread GetReady_Trigger
	self anim_attached 11J805_JumpmasterGetReady
	self waittill animdone
//*/

	thread StandUp_Trigger
	self anim 11J806_JumpmasterStandUp
	self waittill animdone

	self thread global/LoopAnim.Scr::LoopAnim 11J904_JumpmasterWait

	wait 1

///*
	thread HookUp_Trigger

	self thread global/LoopAnim.Scr::LoopAnimStop

	self anim 11J807_JumpmasterHookUp
	self waittill animdone

	self thread global/LoopAnim.Scr::LoopAnim 11J904_JumpmasterWait

	wait 1
//*/

///*
	self anim 11J903_JumpmasterCheck
	self waittill animdone

	self thread global/LoopAnim.Scr::LoopAnim 11J904_JumpmasterWait

	self thread CheckGear_Trigger

	wait 2.5

	self thread global/LoopAnim.Scr::LoopAnimStop

	// tell them to sound off..
	self anim 11J808_JumpmasterSoundOff
	self waittill animdone
	self thread global/LoopAnim.Scr::LoopAnim 11J904_JumpmasterWait
	self waitthread SoundOff_Trigger

//*/
	thread HellYes_Trigger
	self anim 11J809_JumpmasterHappy
	self waittill animdone

///*
	thread FlakExplosion_Trigger
	self anim 11J810_JumpmasterSticks
	self waittill animdone
	thread JumpLightGreen

//*/
	self anim_attached 11J901_JumpmasterStartGo
	self waittill animdone

	$world farclipoverride 16000

	// make everyone jump...
	self waitthread DoJump_Trigger


End

//---------------------------------------------------------------------------------
JumpLightRed:
//---------------------------------------------------------------------------------

	$jumplight_red anim on

End

//---------------------------------------------------------------------------------
JumpLightGreen:
//---------------------------------------------------------------------------------

	$jumplight_red delete
	$jumplight_green anim on
	$searchlights show
	$clouds delete

End


//---------------------------------------------------------------------------------
DoWelcomeToFrance:
//---------------------------------------------------------------------------------

	// UNCOMMENT THESE NEXT TWO LINES WHEN KURY GETS THE .SKC FILES READY
	for ( local.i=3;local.i<=8;local.i++ )
		$airborne[local.i] thread LookAtFrance

	$airborne[2] thread global/LoopAnim.Scr::LoopAnimStop
	$airborne[2] anim 11A216_airman02_FranceBoys
	thread Lookat_Jumpmaster
	$airborne[2] waittill animdone
//	$airborne[2] lookat $lookat_marker_passengerside
//	wait 4
//	$airborne[2] lookat NULL
	$airborne[2] thread global/LoopAnim.Scr::LoopAnim $airborne[2].idle


End

//---------------------------------------------------------------------------------
LookAtFrance:
//
//	Have the airmen react to the welcome to france deal...
//---------------------------------------------------------------------------------

	if ( self.look_at_france==NIL )
		end

	local.random = randomfloat (1)

	// make airborne4 react sooner.
	if ( self==$airborne[4] )
		local.random = 1.9
	else
		local.random += 2

	wait local.random

	self thread global/LoopAnim.scr::LoopAnimStop
	self anim self.look_at_france
	self waittill animdone

	self thread global/LoopAnim.scr::LoopAnim self.idle


end
//---------------------------------------------------------------------------------
Lookat_Jumpmaster:
//---------------------------------------------------------------------------------

	thread Lookat_Jumpmaster1
//	thread Lookat_Jumpmaster2
//	thread Lookat_Jumpmaster3
	
End

//---------------------------------------------------------------------------------
Lookat_Jumpmaster1:
//---------------------------------------------------------------------------------
		wait 0.5
		$airborne[4] lookat $lookat_marker_passengerside
		$airborne[7] lookat $lookat_marker_passengerside
		wait 3
		$airborne[4] lookat NULL
		wait 2
		$airborne[7] lookat NULL
End

//---------------------------------------------------------------------------------	
Lookat_Jumpmaster2:
//---------------------------------------------------------------------------------
		wait 0.75
		$airborne[5] lookat $lookat_marker_passengerside
		$airborne[9] lookat $lookat_marker_passengerside
		wait 3
		$airborne[9] lookat NULL
		wait 2
		$airborne[5] lookat $airborne[2]
		wait 4
		$airborne[5] lookat NULL
	
End

//---------------------------------------------------------------------------------	
Lookat_Jumpmaster3:
//---------------------------------------------------------------------------------
		wait 0.5
		$airborne[3] lookat $airborne[2]
		$airborne[6] lookat $airborne[2]
		$airborne[8] lookat $airborne[2]
		$airborne[10] lookat $airborne[2]
		wait 3
		$airborne[3] lookat NULL
		$airborne[6] lookat NULL
		$airborne[8] lookat NULL
		$airborne[10] lookat NULL
	
End

//---------------------------------------------------------------------------------
SetupPlayer:
//---------------------------------------------------------------------------------
	$player physics_off
	$player glue $playerspot_plane 0
	$playerspot_plane time 0.001
	$playerspot_plane movedown 64
	$playerspot_plane move

	$c47_1 hide
	$c47_2 hide
	$c47_4 hide
	$c47_5 hide
	$c47_6 hide
	$c47_8 hide
	$c47_9 hide
	$c47_11 hide
	$c47_12 hide
	$c47_13 hide
	$c47_14 hide
	$c47_15 hide
	$c47_16 hide
	$c47_17 hide
	$c47_bank hide
	$c47_crash1 hide
	$c47_crash2 hide
	$searchlights hide

	thread camerashake 0 68 0.1 1 1
	thread camerashake 68.5 11.5 1 1 1
end

//---------------------------------------------------------------------------------
camerashake local.delay local.duration local.magnitude local.no_rampup local.no_rampdown:
//---------------------------------------------------------------------------------
	// wait for this long before you enable the effect
	if (local.delay > 0)
		wait local.delay

	exec global/earthquake.scr local.duration local.magnitude local.no_rampup local.no_rampdown

End

//---------------------------------------------------------------------------------
PlayerStandUp:
//---------------------------------------------------------------------------------
	if ( level.bSkipPlaneRide==0 )
		wait 0.5

	if ( level.bSkipPlaneRide==0 )
		$playerspot_plane time 1

	$playerspot_plane moveeast 35
	$playerspot_plane moveup 34
	$playerspot_plane movesouth 15
	$playerspot_plane move

End

//---------------------------------------------------------------------------------
PlayerMoveForward:
//---------------------------------------------------------------------------------
	if ( level.bSkipPlaneRide==0 )
		$playerspot_plane time 1

	$playerspot_plane movenorth 18
	$playerspot_plane move

End

//---------------------------------------------------------------------------------
PlayerJump:
//---------------------------------------------------------------------------------
	level.justjumped = 1 // used in t1l1_misc

	if ( level.bSkipPlaneRide==0 )
		$playerspot_plane time 1

	$playerspot_plane movenorth 36
	$playerspot_plane move

	wait 1

	// force player to look where we want them to...
	freezeplayer
	thread gags/T1L1_Falling.scr::PlayerFaceYawPitch 42 0

	$playerspot_plane moveeast 160
	$playerspot_plane move

	wait 1

	thread Cleanup


	// PlayerFall will glue us back...
	$player unglue

	thread gags/T1L1_Falling.Scr::noparachute_guy $noparachute_guy_start $noparachute_guy_dest

	
	// let the falling script takeover from here...
	waitthread gags/t1l1_falling.scr::PlayerFall
	exec gags/t1l1_barn_crash.scr::start

	// wait a couple of seconds and send the no parachute guy out...
	
End

//---------------------------------------------------------------------------------
Setup1 local.idle local.standup local.stand_idle local.hookup local.startcheck local.seat:
//---------------------------------------------------------------------------------
	self exec global/disable_ai.scr

	self physics_off

	self.idle			= local.idle
	self.standup		= local.standup
	self.stand_idle		= local.stand_idle
	self.hookup			= local.hookup
	self.startcheck		= local.startcheck

	self attach $the_plane local.seat 1 ( 0 0 0 )

	self.check_anim_attach = 0

End

//---------------------------------------------------------------------------------
Setup2 local.checkcycle local.soundoff local.walk local.jump local.balance:
//---------------------------------------------------------------------------------
	self.checkcycle		= local.checkcycle
	self.soundoff		= local.soundoff
	self.walk			= local.walk
	self.jump			= local.jump
	self.balance		= local.balance

End

//---------------------------------------------------------------------------------
Setup3 local.react local.hell_yes local.trans_look local.look_idle local.stand_hook:
//---------------------------------------------------------------------------------

	self.react			= local.react
	self.hell_yes		= local.hell_yes
	self.trans_look		= local.trans_look
	self.look_idle		= local.look_idle
	self.stand_hook		= local.stand_hook

	// startoff looping out sitting down idle...
	self thread global/LoopAnim.Scr::LoopAnim self.idle

End

//---------------------------------------------------------------------------------
AirborneTest:
//---------------------------------------------------------------------------------

	for (local.i=1;local.i<=$airborne.size;local.i++)
	{
		$airborne[local.i] waitthread AirborneTestAnim
	}
	
End

//---------------------------------------------------------------------------------
AirborneTestAnim:
//---------------------------------------------------------------------------------

	self anim self.hell_yes
	self waittill animdone
	wait 0.2
	
End

//---------------------------------------------------------------------------------
GetReady_Trigger:
//---------------------------------------------------------------------------------
	wait 1.5
	for (local.i=1;local.i<=$airborne.size;local.i++)
	{
		$airborne[local.i] thread GetReady
	}

End

//---------------------------------------------------------------------------------
GetReady:
//---------------------------------------------------------------------------------

	if ( self.trans_look==NIL )
			End

	wait (randomfloat 0.5)

	self waitthread global/LoopAnim.Scr::LoopAnimStop
	
	self anim self.trans_look
	self waittill animdone

	self waitthread global/LoopAnim.Scr::LoopAnim self.look_idle

End

//---------------------------------------------------------------------------------
StandUp_Trigger:
//
//	Tell everyone to standup...
//
//---------------------------------------------------------------------------------

	wait 1.5
	for (local.i=1;local.i<=$airborne.size;local.i++)
	{
		$airborne[local.i] thread StandUp
	}

	$player thread PlayerStandUp

End

//---------------------------------------------------------------------------------
StandUp:
//---------------------------------------------------------------------------------

	wait (randomfloat 0.75)

	self thread global/LoopAnim.Scr::LoopAnimStop

	self anim_attached self.standup
	self waittill animdone

//	self turnto $jumpmaster
	self thread global/LoopAnim.Scr::LoopAnim self.stand_idle

	
End

//---------------------------------------------------------------------------------
HookUp_Trigger:
//
//	Tell everyone to HookUp...
//
//---------------------------------------------------------------------------------

	wait 1.5
	for (local.i=1;local.i<=$airborne.size;local.i++)
	{
		$airborne[local.i] thread HookUp
	}

End

//---------------------------------------------------------------------------------
HookUp:
//---------------------------------------------------------------------------------

	wait (randomfloat 0.75)

	self waitthread global/LoopAnim.Scr::LoopAnimStop

	self anim self.hookup
	self waittill animdone

	self thread global/LoopAnim.Scr::LoopAnim self.stand_hook

	
End

//---------------------------------------------------------------------------------
CheckGear_Trigger:
//
//	Have each guy check their partner's gear...
//---------------------------------------------------------------------------------

	for (local.i=$airborne.size;local.i>0;local.i--)
	{
		$airborne[local.i] thread CheckGear
	}


End

//---------------------------------------------------------------------------------
CheckGear:
//---------------------------------------------------------------------------------

	wait (randomfloat 0.75)

	self waitthread global/LoopAnim.Scr::LoopAnimStop

	if (self.check_anim_attach == 1)
		self anim_attached self.startcheck
	else
		self anim self.startcheck

	self waittill animdone

	if (self.check_anim_attach == 1)
		self thread global/LoopAnim.Scr::LoopAnim2 self.checkcycle
	else
		self thread global/LoopAnim.Scr::LoopAnim self.checkcycle

End

//---------------------------------------------------------------------------------
SoundOff_Trigger:
//---------------------------------------------------------------------------------

	// 12-11
//	$player playsound dfr_T1L1_AS2001

	for (local.i=$airborne.size;local.i>0;local.i--)
	{
//		$airborne[local.i] waitthread SoundOff
		$airborne[local.i] thread SoundOff
		if ( local.i==5 )
			wait 2.0
		else
			wait 0.8
	}

End

//---------------------------------------------------------------------------------
SoundOff:
//---------------------------------------------------------------------------------

	self thread global/LoopAnim.Scr::LoopAnimStop

	if (self.check_anim_attach == 1)
		self anim_attached self.soundoff
	else
		self anim self.soundoff

	self waittill animdone

	self thread global/LoopAnim.Scr::LoopAnim self.stand_hook

End

//---------------------------------------------------------------------------------
HellYes_Trigger:
//---------------------------------------------------------------------------------

	wait 2.25

	for (local.i=$airborne.size;local.i>0;local.i--)
	{
		$airborne[local.i] thread HellYes
	}


End

//---------------------------------------------------------------------------------
HellYes:
//---------------------------------------------------------------------------------

	wait (randomfloat 0.3)

	self thread global/LoopAnim.Scr::LoopAnimStop

	self anim self.hell_yes
	self waittill animdone

	self thread global/LoopAnim.Scr::LoopAnim self.stand_hook

End

//---------------------------------------------------------------------------------
FlakExplosion_Trigger:
//
//	Do the flak explosion effect and animation on the squad..
//---------------------------------------------------------------------------------
//	$plane_explosion1 anim start
	$player playsound explode_truck

	wait 0.25

	$plane_explosion2 anim start
	$player playsound explode_truck

	thread boom

	for (local.i=1;local.i<=$airborne.size;local.i++)
	{
		$airborne[local.i] thread FlakExplosion
	}

	// stop our idle
	//$the_plane thread global/LoopAnim.Scr::LoopAnimStop

	$player playsound skydive

	// rock the plane
	//$the_plane anim rock
	//$the_plane waittill animdone

	thread Obscure

	// go back to idle...
	//$the_plane thread global/LoopAnim.Scr::LoopAnim idle

End


//---------------------------------------------------------------------------------
boom:
//	Shake the player like hell
//---------------------------------------------------------------------------------

	if ( level.bSkipPlaneRide==0 )
		$playerspot_plane time .5

	thread camerashake 0 .75 7 0 1

end





//---------------------------------------------------------------------------------
FlakExplosion:
//---------------------------------------------------------------------------------

	wait (randomfloat 0.5)

	self waitthread global/LoopAnim.Scr::LoopAnimStop

	self anim_attached self.react
	self waittill animdone

	self anim_attached self.balance
	self waittill animdone

	self thread global/LoopAnim.Scr::LoopAnim self.stand_hook

End

//---------------------------------------------------------------------------------
DoJump_Trigger:
//---------------------------------------------------------------------------------

	self thread DoGoGoGo

	thread Flak_on

	for (local.i=1;local.i<=5;local.i++)
	{
		if ( local.i==1 )
		{
			thread Walk_Trigger (local.i+2)
			thread PlayerMoveForward
			$airborne[1] thread Jump
			wait 0.5
			$airborne[2] thread Jump
			local.i++
			wait 1
		}
		else
		{
			if ( local.i <= 4 )
			{
				thread Walk_Trigger (local.i+1)
				thread PlayerMoveForward
				$airborne[local.i] thread Jump
			}
			else
			{
				thread Walk_Trigger (local.i)
				thread PlayerJump
			}
			wait 1.25
		}

	}

End

//---------------------------------------------------------------------------------
Walk_Trigger local.start:
//---------------------------------------------------------------------------------

	for (local.i=local.start;local.i<=$airborne.size;local.i++)
	{
		$airborne[local.i] thread Walk
	}

End

//---------------------------------------------------------------------------------
Walk:
// take our step forward...
//---------------------------------------------------------------------------------
//	wait (randomfloat 0.2)

	self thread global/LoopAnim.Scr::LoopAnimStop

	self anim_attached self.walk
	self waittill animdone

	self thread global/LoopAnim.Scr::LoopAnim self.stand_hook

End
//---------------------------------------------------------------------------------
DoGoGoGo:
//---------------------------------------------------------------------------------
	self waitthread global/LoopAnim.Scr::LoopAnimStop

	// yell go go go
	self anim_attached 11J902_JumpmasterGoGoGo
	self waittill animdone

//start the sky portal show
	thread Sky_portal_show

	wait 1.0
	self anim_attached 11J902_JumpmasterGoGoGo
	self waittill animdone


	
	// wait for them to go...
	self thread global/LoopAnim.Scr::LoopAnim2 11J905_JumpmasterGoCycle

End

//---------------------------------------------------------------------------------
KickMe:
//	Kick the guy out of the plane..
//---------------------------------------------------------------------------------

	wait 0.5
	self waitthread global/LoopAnim.Scr::LoopAnimStop
	self anim 11J804_JumpmasterKickGuy
	self waittill animdone

	wait 3

	// back to idle...
	self thread global/LoopAnim.Scr::LoopAnim 11J803_JumpmasterStandIdle

End

//---------------------------------------------------------------------------------
Jump:
//	Loop our walk animation until we hit the jump trigger....
//	the jump trigger will set bJumpTime==1
//---------------------------------------------------------------------------------

	self waitthread global/LoopAnim.Scr::LoopAnimStop

		// this was cut!
//	if ( self==$airborne[4] )
//	{
//		$jumpmaster thread KickMe
//	}

	self anim_attached self.jump
	self waittill animdone

	self notsolid
	self hide
//	self delete

End

//---------------------------------------------------------------------------------
Fly local.path local.speed local.scale:
//---------------------------------------------------------------------------------

	self.fly_thread = local
	self scale local.scale
	self flypath local.path local.speed 2000 256
	self waitmove
	self delete

end

//---------------------------------------------------------------------------------
FlyC47s:
//---------------------------------------------------------------------------------
	$c47_1 show
	$c47_2 show
	$c47_4 show
	$c47_5 show
	$c47_6 show
	$c47_8 show
	$c47_9 show
	$c47_11 show
	$c47_12 show
	$c47_13 show
	$c47_14 show
	$c47_15 show
	$c47_16 show
	$c47_17 show
	$c47_bank show
	$c47_crash1 show
	$c47_crash2 show

	// path name is followed by speed and scale
	$c47_1 thread Fly $c47_1_path 250 0.3
	$c47_2 thread Fly $c47_2_path 300 0.3
	$c47_4 thread Fly $c47_4_path 325 0.25
	$c47_5 thread Fly $c47_5_path 325 0.4
	$c47_6 thread Fly $c47_6_path 250 0.2
	$c47_8 thread Fly $c47_8_path 260 0.1
	$c47_9 thread Fly $c47_9_path 240 0.1
	$c47_11 thread Fly $c47_11_path 100 0.17
	$c47_12 thread Fly $c47_12_path 100 0.2
	$c47_13 thread Fly $c47_13_path 100 0.15
	$c47_14 thread Fly $c47_14_path 100 0.1
	$c47_15 thread Fly $c47_15_path 175 0.35
	$c47_16 thread Fly $c47_16_path 100 0.15
	$c47_17 thread Fly $c47_17_path 100 0.1

	$c47_crash1 thread C47_AAGotYou
	$c47_crash1 thread Fly $c47_crash1_path 225 0.5

	$c47_crash2 thread C47_GetCrashedInto
	$c47_crash2 thread Fly $c47_crash2_path 225 0.35

	wait 2
	$c47_bank thread Fly $c47_bank_path 260 0.3
	thread C47_DoBank

End

//---------------------------------------------------------------------------------
Parchuteguys:
//---------------------------------------------------------------------------------

	thread gags/T1L1_Falling.Scr::parachute_guy_near $trooper_near_1 31 0 0 0.5
	thread gags/T1L1_Falling.Scr::parachute_guy_near $trooper_near_2 31 0 0 0.4
	thread gags/T1L1_Falling.Scr::parachute_guy_near $trooper_near_3 31 0 0 0.6
	thread gags/T1L1_Falling.Scr::parachute_guy_near $trooper_near_4 32 0 0 0.5
	thread gags/T1L1_Falling.Scr::parachute_guy_near $trooper_near_5 30 0 0 0.7
	thread gags/T1L1_Falling.Scr::parachute_guy_near $trooper_near_6 32 0 0 0.5

End

//---------------------------------------------------------------------------------
C47_DoBank:
//
//	gags/t1l1_PlaneRide.Scr::C47_DoBank
//
//	have plane bank to avoid flak explosions...
//---------------------------------------------------------------------------------

	// note: change this to be in front of the plane once it works...
	wait 11
	local.fx = spawn script_model model "models/fx/exp_flak_near.tik"

	local.fx origin ($c47_bank.origin + (900 0 50) )

	local.fx notsolid
	local.fx anim start

	$c47_bank anim bank
	$c47_bank waittill animdone
	$c47_bank anim idle

	local.fx remove

//	fadeout .001 0 0 0 1


End
	
//---------------------------------------------------------------------------------
C47_AAGotYou:
//
//		Get hit by AA fire and crash into a plane below us...
//---------------------------------------------------------------------------------
	$aa_c47 TurnOn

	self playsound t1_planecrash
	
	wait 9.06

	spawn script_model "model" models/fx/fx_truck_explosion.tik "origin" self.origin

	// delete our flythread...
	if ( self.fly_thread!=NIL )
		self.fly_thread delete

	// stop us.
	self stop

	// change our model and do the crash anim
	self model models/vehicles/c47_damage_wing.tik
	self anim crash
	self waittill animdone

	self delete

End

//---------------------------------------------------------------------------------
C47_GetCrashedInto:
//
//		We've been hit by a plane above us, blow up and split in two!
//---------------------------------------------------------------------------------

	wait 11
	// do explosion...			
	spawn script_model "model" models/fx/C47_Collision_fire.tik "origin" self.origin
	spawn script_model "model" models/fx/C47_Collision_smoke.tik "origin" self.origin
	wait .2
	spawn script_model "model" models/fx/fx_truck_explosion.tik "origin" self.origin

	// delete our flythread...
	if ( self.fly_thread!=NIL )
		self.fly_thread delete

	// stop us.
	self stop

	// change our model and do the crash anim
	self model models/vehicles/c47_damage_tail.tik
	self anim crash
	self waittill animdone
	self delete

End

//-------------------------------------------------------------------
Flak_on: 
//-------------------------------------------------------------------

	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_1 models/fx/exp_flak_far.tik  1  3

	thread gags/box_effects.scr::execute_fx_region $flak_sys_2 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_2 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_2 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_2 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_2 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_2 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_2 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_2 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_2 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_2 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_2 models/fx/exp_flak_far.tik  1  3
	thread gags/box_effects.scr::execute_fx_region $flak_sys_2 models/fx/exp_flak_far.tik  1  3
	
//-------------------------------------------------------------------
// Player Flak System 
//-------------------------------------------------------------------																

	thread gags/box_effects.scr::execute_player_fx models/fx/exp_flak_near.tik  1  5 300 1000
	thread gags/box_effects.scr::execute_player_fx models/fx/exp_flak_near.tik  1  5 300 1000
	thread gags/box_effects.scr::execute_player_fx models/fx/exp_flak_near.tik  1  5 300 1000
End

//-------------------------------------------------------------------
// Obscuring Flak System
//-------------------------------------------------------------------

//---------------------------------------------------------------------------------
Obscure:
//---------------------------------------------------------------------------------

	thread gags/box_effects.scr::execute_target_flak models/fx/exp_flak_near.tik $flakspot1 35
	thread gags/box_effects.scr::execute_target_flak models/fx/exp_flak_near.tik $flakspot2 36
	thread gags/box_effects.scr::execute_target_flak models/fx/exp_flak_near.tik $flakspot3 36.5
	thread gags/box_effects.scr::execute_target_flak models/fx/exp_flak_near.tik $flakspot4 36.75
	thread gags/box_effects.scr::execute_target_flak models/fx/exp_flak_near.tik $flakspot5 37.25
	thread gags/box_effects.scr::execute_target_flak models/fx/exp_flak_near.tik $flakspot6 38

	thread gags/box_effects.scr::execute_target_flak models/fx/exp_flak_near.tik $flakspot7 35
	thread gags/box_effects.scr::execute_target_flak models/fx/exp_flak_near.tik $flakspot8 36
	thread gags/box_effects.scr::execute_target_flak models/fx/exp_flak_near.tik $flakspot9 36.5
	thread gags/box_effects.scr::execute_target_flak models/fx/exp_flak_near.tik $flakspot10 36.75
	thread gags/box_effects.scr::execute_target_flak models/fx/exp_flak_near.tik $flakspot11 37.25
	thread gags/box_effects.scr::execute_target_flak models/fx/exp_flak_near.tik $flakspot12 38

	wait 29
//	dprintln "starting seqence in 5 seconds"

End

//-------------------------------------------------------------------
// Particle AA Fire in Skyportal
Sky_portal_show:
//-------------------------------------------------------------------
	$tracer_1 anim start	
	$trooper_pooper origin "7376 7376 7170"
	$trooper_pooper anim start
	$c47_billboards origin "7376 7376 7170"
	$c47_billboards anim start

	thread gags/t1l1_misc.scr::searchlights
	wait 3
	thread FlyC47s
	thread gags/T1L1_Falling.Scr::TurnOnAAGuns
	thread Parchuteguys

End