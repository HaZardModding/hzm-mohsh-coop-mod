//-------------------------------------------------------------------
main:
//-------------------------------------------------------------------

	thread SetupFlakCannons
	level.bKillCappy	= 0
End

//-------------------------------------------------------------------
EnableFlakGuys:
//-------------------------------------------------------------------

	$FlakGerman4 exec global/enable_ai.scr
	$FlakGerman5 exec global/enable_ai.scr
	$FlakGerman6 exec global/enable_ai.scr

	$FlakGerman4 threatbias 5000
	$FlakGerman5 threatbias 5000
	$FlakGerman6 threatbias 5000

End

//-------------------------------------------------------------------
SetupFlakCannons:
//-------------------------------------------------------------------
	
	$Flak88A.gun = $Flak88A QueryTurretSlotEntity 0
	$Flak88B.gun = $Flak88B QueryTurretSlotEntity 0

	$Flak88A.gun setaimtarget $Flak88A.target
	$Flak88B.gun setaimtarget $Flak88B.target

	// get it firing until the grenade in the barn gag...
	$Flak88A thread FireFlak88Loop

	$FlakGerman4 ai_off
	$FlakGerman5 ai_off
	$FlakGerman6 ai_off

	$FlakGerman4 threatbias ignoreme
	$FlakGerman5 threatbias ignoreme
	$FlakGerman6 threatbias ignoreme

End

//-------------------------------------------------------------------
DoFlak88Guys:
//
//		Sets up and gets the guys visually firing the flak88...
//-------------------------------------------------------------------

	$FlakGerman4 ai_on
	$FlakGerman5 ai_on
	$FlakGerman6 ai_on

	$FlakGerman4.no_idle = 1
	$FlakGerman5.no_idle = 1
	$FlakGerman6.no_idle = 1

	$FlakGerman4.FlakLoad			= 12G101_FlakLoad1
	$FlakGerman4.FlakLoad2Cover		= 12G102_FlakLoad2Cover
	$FlakGerman4.FlakCover			= 12G103_FlakCover
	$FlakGerman4.FlakCover2Load		= 12G104_FlakCover2Load

	$FlakGerman5.FlakLoad			= 12G201_FlakLoad2
	$FlakGerman5.FlakLoad2Cover		= 12G202_FlakLoad2Cover
	$FlakGerman5.FlakCover			= 12G203_FlakCover
	$FlakGerman5.FlakCover2Load		= 12G204_FlakCover2Load

	$FlakGerman4.angles = "0 -90 0"
	$FlakGerman5.angles = "0 180 0"
	$FlakGerman6.angles = "0 180 0"

	$FlakGerman4.origin = $Flak88B gettagposition "passenger2"
	$FlakGerman5.origin = $Flak88B gettagposition "passenger1"
	$FlakGerman6.origin = $Flak88B gettagposition "passenger3"

	$FlakGerman5.origin = ( $FlakGerman5.origin + (20 50 0) )

//	$FlakGerman4 attach $Flak88B passenger1 0 ( 0 0 0 )
//	$FlakGerman5 attach $Flak88B passenger2 0 ( 0 0 0 )
//	$FlakGerman6 attach $Flak88B passenger3 0 ( 0 0 0 )


	$FlakGerman4 thread DoFlakGuy 1
	$FlakGerman5 thread DoFlakGuy 0

	// #6 is the officer...
	$FlakGerman6 thread DoFlakOfficer

	$FlakGerman4 thread CancelGagWait
	$FlakGerman5 thread CancelGagWait
	$FlakGerman6 thread CancelGagWait

	// captain will update this after he says his shiznit...
	$FlakGerman4 nodamage
	$FlakGerman5 nodamage
	$FlakGerman6 nodamage

	// disable their ai until ready for the gag...
	$FlakGerman4 exec global/disable_ai.scr
	$FlakGerman5 exec global/disable_ai.scr
	$FlakGerman6 exec global/disable_ai.scr

	$FlakGerman4 hearing 1
	$FlakGerman5 hearing 1
	$FlakGerman6 hearing 1
	
	$FlakGerman4 sight 1
	$FlakGerman5 sight 1
	$FlakGerman6 sight 1



End

//-------------------------------------------------------------------
StopFlak88Guys:
//
//	Stop shooting the flak and go after cappy...
//-------------------------------------------------------------------

	level.bFlakCancelled = 1

	waitthread EnableFlakGuys

	// crank up their hearing and sight...
	$FlakGerman4 hearing 5000
	$FlakGerman5 hearing 5000
	$FlakGerman6 hearing 5000

	$FlakGerman4 sight 5000
	$FlakGerman5 sight 5000
	$FlakGerman6 sight 5000

	$FlakGerman4.flakthread delete
	$FlakGerman5.flakthread delete

	$FlakGerman4 waitthread global/LoopAnim.Scr::LoopAnimStop
	$FlakGerman5 waitthread global/LoopAnim.Scr::LoopAnimStop
	$FlakGerman6 waitthread global/LoopAnim.Scr::LoopAnimStop

	$FlakGerman4 detach
	$FlakGerman5 detach
	$FlakGerman6 detach

	$FlakGerman4 unholster
	$FlakGerman5 unholster

	$FlakGerman4 takedamage
	$FlakGerman5 takedamage
	$FlakGerman6 takedamage

	$FlakGerman4 solid
	$FlakGerman5 solid
	$FlakGerman6 solid



End

//-------------------------------------------------------------------
DoFlakGuy local.bFireFlak:
//-------------------------------------------------------------------

	//
	//self exec global/disable_ai.scr
	//

	self.flakthread = local
	
	self holster

	while ( isAlive self )
	{
		self anim self.FlakLoad
		self waittill animdone

		if ( local.bFireFlak==1 )
		{
			$FlakGerman6 thread OfficerLook
			$Flak88B thread Fire level.bKillCappy
		}

		self anim self.FlakLoad2Cover
		self waittill animdone

		self thread global/LoopAnim.Scr::LoopAnim self.FlakCover

		wait 1

		self thread global/LoopAnim.Scr::LoopAnimStop

		self anim self.FlakCover2Load
		self waittill animdone
	}

End

//-------------------------------------------------------------------
OfficerIdle:
//-------------------------------------------------------------------

	self thread global/LoopAnim.Scr::LoopAnimStop
	self thread global/LoopAnim.Scr::LoopAnim 12G301_Officeridle

End

//-------------------------------------------------------------------
OfficerLook:
//-------------------------------------------------------------------

	self thread global/LoopAnim.Scr::LoopAnimStop
	self anim 12G302_Officeridle2Look
	self waittill animdone
	
	self anim 12G303_OfficerLook
	self waittill animdone

	self anim 12G304_OfficerLook2Idle
	self waittill animdone

	self thread OfficerIdle


End

//-------------------------------------------------------------------
DoFlakOfficer:
//-------------------------------------------------------------------
	//self exec global/disable_ai.scr

	self thread OfficerIdle

End

//-------------------------------------------------------------------
CancelGagWait:
//
//	if any of the flak guys get shot, we'll go take out the captain
//  unless the captain has started firing at us...
//-------------------------------------------------------------------

	level.bFlakCancelled = 0

	self thread CancelGagOnDeath

	self waittill pain

	thread CancelGag

End

//-------------------------------------------------------------------
CancelGagOnDeath:
//-------------------------------------------------------------------

	self waittill death

	thread CancelGag

end

//-------------------------------------------------------------------
CancelGag:
//	
//-------------------------------------------------------------------
	if ( level.bFlakCancelled == 1 )
			End

	level.bFlakCancelled = 1

	// they didn't wait, therefore takeout the captain...
	dprintln "Taking out the captain!"


	// let it move baby!
	$Flak88B_turret0 turnspeed 45
	$Flak88B_turret0 pitchspeed 7.5
	$Flak88B_turret0 viewjitter 7
	$Flak88B_turret0 firedelay 4
	$Flak88B_turret0 maxyawoffset 180.0
	$Flak88B_turret0 pitchcaps "90 90 0"

	level.bKillCappy	= 1

	// take out the captain right now! Keep these two on the machine...
	$FlakGerman4 nodamage
	$FlakGerman5 nodamage

	$FlakGerman6 hearing 5000
	$FlakGerman6 sight 5000
	$FlakGerman6 waitthread global/LoopAnim.Scr::LoopAnimStop
	$FlakGerman6 detach

	$FlakGerman6 exec global/enable_ai.scr
	$FlakGerman6 runto $player

End

//-------------------------------------------------------------------
FireFlak88Loop local.min_time local.rand_time:
//	Just plays the flak88 fire animation at a regular interval..
//	( for inside the barn )
//-------------------------------------------------------------------

	self.loop_thread = local

	if ( local.min_time==NIL )
		local.min_time = 3

	if ( local.rand_time==NIL )
		local.rand_time = 8

	while ( isAlive self )
	{
		// how quickly to fire?  
		local.random = local.min_time + ( randomfloat local.rand_time )
		wait local.random
		self waitthread Fire 0
		wait 0.5
		self.gun sound flak_snd_reload
	}

			End

//-------------------------------------------------------------------
StopFlak88Loop:
//-------------------------------------------------------------------

	self.loop_thread delete

End

//-------------------------------------------------------------------
Fire local.bKillCappy:
//	Flak88 firing...
//-------------------------------------------------------------------


	if ( local.bKillCappy==1 )
	{
		self.gun setaimtarget level.cappy
	}
	else
	{
		self.gun setaimtarget self.target
	}

	wait 0.75

	self.gun waittill ontarget
	self.gun anim fire

	if ( local.bKillCappy==1 )
	{
		// killing captain now...
		wait 0.1
		level.cappy takedamage
		level.cappy health 1
		level.cappy hurt 999
		level.cappy damage self 9000 self (0 0 0) (0 0 0) (0 0 0) 0 9 0 0

		radiusdamage level.cappy.origin 600 900 1
	}

End

