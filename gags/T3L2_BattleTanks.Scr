//-----------------------------------------------------------------
//	T3L2_BattleTanks.Scr
//
//		Tank AI for the nazi tanks at the final battle...
//
//-----------------------------------------------------------------


//-----------------------------------------------------------------
main:
//-----------------------------------------------------------------

	end


//-----------------------------------------------------------------
BattleTankAI:
//
//	Randomly pick between the 2 friendly tanks and the player tank
//  and shoot at them...
//-----------------------------------------------------------------

	// remove our current attack thread if needed...
	if ( self.attack_thread!=NIL && self.attack_thread!=NULL )
	{
		self.attack_thread delete
		self.attack_thread = NULL
	}

	if ( self.gun==NIL || self.gun==NULL )
		self.gun = self QueryTurretSlotEntity 0

	while ( isAlive self )
	{
		local.waittime = randomint ( 1.5 )
		local.waittime += 3
		wait local.waittime

		while ( self.stunned==1 )
		{
			wait 0.25
		}
		self waitthread GetTarget
		self.gun anim fire
	}

	end

//-----------------------------------------------------------------
GetTarget:
//
//	choose between the player and the other 2 allied tanks...
//-----------------------------------------------------------------

	// first decide if we want to hit or miss...

	local.random = randomint (5)

	if ( $german_tank_target==NIL || $german_tank_target.size==0 )
		local.random = 0

	// 1 in 5 chance we'll hit...
	if ( local.random==0 )
	{

		local.random = randomint (2)

		switch ( local.random )
		{
			case	0:
				if ( isAlive $t34_friendly1 )
				{
					local.target = $t34_friendly1
					break;
				}
				// else fall thru here...
			case	1:
				if ( isAlive t34_friendly2 )
				{
					local.target = $t34_friendly2
					break;
				}

				if ( isAlive $t34_friendly1 )
				{
					local.target = $t34_friendly1
					break;
				}
			default:
				local.target = $player
				break;
		}
	}
	else
	{
		local.random = randomint ( $german_tank_target.size )

		if ( local.random==0 )
			local.random++

		local.target = $german_tank_target[local.random]
	}

	// lock on target..
	self.gun setAimTarget local.target
	self.gun waittill ontarget
	local.waittime = randomfloat ( 0.5 )
	local.waittime += 0.2

	wait local.waittime

	end
